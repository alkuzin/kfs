# Monolithic Unix-like kernel from scratch.
# Copyright (C) 2024 Alexander (@alkuzin).
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.


# magic number is a value that BIOS recognize as a kernel
.set MBOOT_MAGIC, 0x1BADB002

# flags can control various functions of the BIOS such as:
# enabling/disabling specific hardware components, setting up boot options,
# adjusting power management settings and etc.
.set MBOOT_PAGE_ALIGN, 1 << 0 # memory should be page-aligned to 4 KB
.set MBOOT_MEM_INFO,   1 << 1 # request memory information from the bootloader
.set MBOOT_USE_GFX,    1 << 2 # enable graphics mode

# indicates a bootable device
.set MBOOT_FLAGS, (MBOOT_PAGE_ALIGN | MBOOT_MEM_INFO | MBOOT_USE_GFX)

# the purpose of checksum is to ensure OS stability & security
# by detecting any unauthorised changes to the BIOS firmware
.set MBOOT_CHECKSUM, -(MBOOT_MAGIC + MBOOT_FLAGS)

.section .multiboot
.align 4
    .long MBOOT_MAGIC       # declare double word of magic number
    .long MBOOT_FLAGS       # declare double word of magic number flags
    .long MBOOT_CHECKSUM    # declare double word of magic number checksum
    .long 0, 0, 0, 0, 0     # declare double words for some fields

    .long 0                 # linear graphics mode
    .long 1024              # screen width
    .long 768               # screen height
    .long 32                # depth

# higher-half kernel virtual base address (3 GB)
.set base_address, 0xC0000000

.section .bss               # stores uninitialized global and static variables
.align 16                   # reserving space for the stack
stack_bottom:
    .skip 65536             # 64 KB
stack_top:

.section .text              # contains executable instructions of a program

.global boot
boot:
    # move initial page directory address to ESI, ECX & EDX
    mov $(initial_page_dir - base_address), %esi
    mov %esi, %ecx
    mov %esi, %edx

    or $0x00000003, %ecx    # set the attributes: read/write & present bits
    mov %ecx, 0xFFC(%edx)   # initial page directory address + 4092
    mov %esi, %cr3          # update the page directory

    mov %cr4, %ecx          # get the control register 4 value
    or $0x00000010, %ecx    # set the PSE (Page Size Extension) bit
    mov %ecx, %cr4          # update CR4 to enable 4 MB pages

    mov %cr0, %ecx          # get the control register 0 value
    or $0x80000000, %ecx    # set the paging bit
    mov %ecx, %cr0          # update CR0 to enable paging

    call higher_half

higher_half:
    mov %cr0, %ecx          # get the control register 0 value
    or $1, %cl              # set the protection mode bit
    mov %ecx, %cr0          # update the control register 0

    mov $stack_top, %esp    # set the stack pointer
    push %ebx               # push multiboot info
    push %eax               # push magic number
    xor %ebp, %ebp          # reset ebp

    .extern kmain           # from kernel/kernel.cpp
    call kmain              # call kernel entry point
    cli                     # clear interrupt flag (disables interrupts)

halt:
    hlt	                    # this instruction halts the CPU
    jmp halt                # infinite loop

.section .data              # stores initialized global and static variables
.align 0x1000

.global initial_page_dir    # used for enabling paging in early boot
initial_page_dir:
# identity map first 3 GB of virtual memory (0x00000000-0xC0000000)
.set i, 0x00000083          # 4 MB page is present & allowed to read/write
.rept 0x300                 # 0x300 = 0xbfc00000/0x00400000 + 1
    .long i
    .set i, i + 0x00400000  # 4 MB
.endr

# map first 4 MB page to (0xC0000000-0xC0400000) from (0x00000000-0x00400000)
.long 0x00000083            # 4 MB page is present & allowed to read/write

# identity map addresses (0xC0400000-0xFFFFFFFF)
.set i, 0xC0400083
.rept 0xFF                  # 0xFF = (0xffc00000 - 0xc0400000)/0x00400000 + 1
    .long i
    .set i, i + 0x00400000  # 4 MB
.endr